apply plugin: 'com.android.application'

android {
    compileSdkVersion project.ext.compileSdkVersion
    buildToolsVersion project.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 21
        manifestPlaceholders = [
                applicationLabelAddition :  ""
        ]
    }

    lintOptions {
        abortOnError false
    }

    def isReleaseAble = System.getenv("SENSORBERG_KEYSTORE_PATH") != null

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'LICENSE.txt'
    }


    signingConfigs {
        if (isReleaseAble) {
            release {
                storeFile file(System.getenv("SENSORBERG_KEYSTORE_PATH"))
                storePassword System.getenv("SENSORBERG_KEYSTORE_PASSWORD")
                keyAlias System.getenv("SENSORBERG_KEYSTORE_KEY_ALIAS")
                keyPassword System.getenv("SENSORBERG_KEYSTORE_PASSWORD")
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "ø"
            minifyEnabled false
            proguardFile getDefaultProguardFile('proguard-android.txt')
            manifestPlaceholders = [
                    applicationLabelAddition :  " Debug ø"
            ]
        }

        release {
            applicationIdSuffix null
            minifyEnabled false
            proguardFile getDefaultProguardFile('proguard-android.txt')
            if (isReleaseAble) {
                signingConfig signingConfigs.release
            }
        }
    }
}

dependencies {
    compile "com.sensorberg.sdk:android-sdk-bootstrapper:2.1.0RC1"
    compile project(':android-sdk')
    compile 'com.google.android.gms:play-services-ads:8.1.0'
}
